buildscript {
    repositories {
        maven {
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
        }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
    }
}

plugins {
    id "net.ltgt.apt" version "0.18"
}
apply plugin: 'java'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def grpcVersion = '1.14.0'
def protobufVersion = '3.5.1'

repositories {
    maven {
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    jcenter()
    mavenLocal()
}

dependencies {
    compileOnly "org.immutables:value:2.7.1:annotations"
    annotationProcessor "org.immutables:value:2.7.1"

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.6'

    compile group: 'net.jodah', name: 'failsafe', version: '1.1.0'

    compile "com.google.api.grpc:proto-google-common-protos:1.0.0"
    compile "io.grpc:grpc-alts:${grpcVersion}"
    compile "io.grpc:grpc-netty-shaded:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "javax.annotation:javax.annotation-api:1.2"
    compile 'com.squareup.okhttp3:okhttp:3.11.0'



    compile "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    compile "xyz.breakit:grpc-api"

    testCompile "io.grpc:grpc-testing:${grpcVersion}"
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
}

apply plugin: 'idea'

apply plugin: 'application'

startScripts.enabled = false

def mainClassName = "xyz.breakit.gateway.Gateway"

jar {
    manifest {
        attributes "Main-Class": "${mainClassName}"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task runClient(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'xyz.breakit.gateway.GatewayClient'
}

def sysprops = System.getProperties()
if (sysprops['idea.platform.prefix'] != null) {
    sourceSets {
        main {
            java {
                srcDir "build/generated/source/apt/main"
            }
        }
    }
}
apply from: '../common/common.gradle'

