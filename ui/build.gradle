import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:7.2.0'
    }
}
plugins {
    id 'idea'
}

private void checkGcpProject() {
    if (!System.getenv().containsKey("GCP_PROJECT")) {
        throw new GradleException("Please set GCP_PROJECT env var before continuing.")
    }
}

apply plugin: 'com.bmuschko.docker-remote-api'

repositories {
    mavenCentral()
}

task buildDocker(type: DockerBuildImage) {
    inputDir = project.projectDir
    images.add("${project.name}:latest")
}

task tagDocker(type: Exec) {
    doFirst { checkGcpProject() }
    dependsOn buildDocker
    commandLine 'docker', 'tag', "${project.name}:latest", "eu.gcr.io/${System.getenv().get("GCP_PROJECT")}/${project.name}:latest"
}

task pushDocker(type: Exec) {
    doFirst { checkGcpProject() }
    dependsOn tagDocker
    commandLine 'gcloud', 'docker', '--', 'push', "eu.gcr.io/${System.getenv().get("GCP_PROJECT")}/${project.name}:latest"
}

task deploy(type: Exec) {
    commandLine 'k8s/deploy.sh'
}

task undeploy(type: Exec) {
    commandLine 'k8s/undeploy.sh'
}